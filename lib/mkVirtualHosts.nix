{
  lib,
  config,
}:
let
  cfg = config.nixflix;

  # --- Caddy helpers ---
  inherit (cfg.caddy) tls;
  tlsDirective = if tls.enable && tls.internal then "tls internal" else "";

  # When TLS is disabled, prefix with http:// to prevent Caddy from
  # automatically enabling HTTPS. "tls off" is not valid Caddyfile syntax.
  caddyHostPrefix = if !tls.enable then "http://" else "";

  # --- Shared theme.park URL builder ---
  themeParkUrl = service: "https://theme-park.dev/css/base/${service}/${cfg.theme.name}.css";

  mkNginxVirtualHost =
    {
      port,
      themeParkService ? null,
      themeParkTag ? "</body>",
      extraConfig ? "",
      stripHeaders ? [ ],
      websocketUpgrade ? false,
      disableBuffering ? false,
    }:
    let
      themeConfig = lib.optionalString (themeParkService != null && cfg.theme.enable) ''
        proxy_set_header Accept-Encoding "";
        sub_filter '${themeParkTag}' '<link rel="stylesheet" type="text/css" href="${themeParkUrl themeParkService}">${themeParkTag}';
        sub_filter_once on;
      '';
      hideHeaders = lib.concatMapStringsSep "\n" (h: ''proxy_hide_header "${h}";'') stripHeaders;
      wsConfig = lib.optionalString websocketUpgrade ''
        proxy_http_version 1.1;
        proxy_set_header Upgrade $http_upgrade;
      '';
      bufferingConfig = lib.optionalString disableBuffering ''
        proxy_buffering off;
      '';
    in
    lib.mkIf cfg.nginx.enable {
      inherit (cfg.nginx) forceSSL;
      useACMEHost = if cfg.nginx.enableACME then cfg.nginx.domain else null;

      locations."/" = {
        proxyPass = "http://127.0.0.1:${toString port}";
        recommendedProxySettings = true;
        extraConfig = ''
          proxy_redirect off;
          ${wsConfig}
          ${bufferingConfig}
          ${hideHeaders}
          ${themeConfig}
          ${extraConfig}
        '';
      };
    };

  mkCaddyVirtualHost =
    {
      port,
      themeParkService ? null,
      themeParkTag ? "</body>",
      extraConfig ? "",
      stripHeaders ? [ ],
      disableBuffering ? false,
    }:
    lib.mkIf cfg.caddy.enable {
      extraConfig = ''
        ${tlsDirective}

        reverse_proxy http://127.0.0.1:${toString port} ${lib.optionalString disableBuffering ''
          {
            flush_interval -1
          }
        ''}

        ${lib.concatMapStringsSep "\n" (h: "header_down -${h}") stripHeaders}

        ${lib.optionalString (themeParkService != null && cfg.theme.enable) ''
          replace {
            ${themeParkTag} "<link rel=\"stylesheet\" type=\"text/css\" href=\"${themeParkUrl themeParkService}\">${themeParkTag}"
          }
          header_up -Accept-Encoding
        ''}

        ${extraConfig}
      '';
    };
in
{
  ## Returns a NixOS config fragment with nginx, caddy, and hosts entries
  ## for a single reverse-proxied service.
  mkVirtualHost =
    {
      hostname,
      expose,
      port,
      themeParkService ? null,
      themeParkTag ? "</body>",
      extraConfig ? "",
      stripHeaders ? [ ],
      websocketUpgrade ? false,
      disableBuffering ? false,
    }:
    let
      proxyArgs = {
        inherit
          port
          themeParkService
          themeParkTag
          extraConfig
          stripHeaders
          disableBuffering
          ;
      };
    in
    {
      services.nginx.virtualHosts.${hostname} = lib.mkIf expose (mkNginxVirtualHost (
        proxyArgs // { inherit websocketUpgrade; }
      ));

      services.caddy.virtualHosts."${caddyHostPrefix}${hostname}" = lib.mkIf expose (
        mkCaddyVirtualHost proxyArgs
      );

      networking.hosts = lib.mkIf (
        expose && cfg.reverseProxy.enable && cfg.reverseProxy.addHostsEntries
      ) { "127.0.0.1" = [ hostname ]; };
    };
}
